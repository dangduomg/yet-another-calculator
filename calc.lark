// yet another calculator
// precedence table (from least to most precedent)
// ASsignment       = += -= *= /= %= **=
// Comparison       == != < <= > >=
// Additive         + -
// Multiplicative   * / %
// Unary            + -
// Power            **

?expr: as_expr

?as_expr: CNAME "=" as_expr -> set
        | CNAME "+=" as_expr -> iadd
        | CNAME "-=" as_expr -> isub
        | CNAME "*=" as_expr -> imul
        | CNAME "/=" as_expr -> idiv
        | CNAME "**=" as_expr -> ipow
        | c_expr

?c_expr: a_expr "==" a_expr -> eq
       | a_expr "!=" a_expr -> ne
       | a_expr "<" a_expr -> lt
       | a_expr "<=" a_expr -> le
       | a_expr ">" a_expr -> gt
       | a_expr ">=" a_expr -> ge
       | a_expr

?a_expr: a_expr "+" m_expr -> add
       | a_expr "-" m_expr -> sub
       | m_expr

?m_expr: m_expr "*" u_expr -> mul
       | m_expr "/" u_expr -> div
       | u_expr

?u_expr: "+" u_expr -> pos
       | "-" u_expr -> neg
       | p_expr

?p_expr: atom "**" u_expr -> pow
       | atom

?atom: num
     | "[" (expr ("," expr)*)? "]" -> array
     | "%[" (shorthand_row (";" shorthand_row)*)? "]" -> array_shorthand
     | CNAME -> var
     | CNAME "(" (expr ("," expr)*)? ")" -> call
     | "(" expr ")"

num: NUMBER

shorthand_row: num+

%import common.NUMBER
%import common.CNAME

%import common.WS
%ignore WS